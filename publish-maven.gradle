apply plugin: 'maven-publish'
apply plugin: 'signing'

group = publishGroupId
version = publishVersion

ext["signing.keyId"] = ''
ext["signing.password"] = ''
ext["signing.secretKeyRingFile"] = ''
ext["ossrhUsername"] = ''
ext["ossrhPassword"] = ''
ext["sonatypeStagingProfileId"] = ''

task javadocJar(type: Jar) {
    archiveClassifier.set('javadoc')
}

File secretPropsFile = project.rootProject.file('local.properties')
if (secretPropsFile.exists()) {
    Properties p = new Properties()
    p.load(new FileInputStream(secretPropsFile))
    p.each { name, value ->
        ext[name] = value
    }
} else {
    ext["signing.keyId"] = System.getenv('SIGNING_KEY_ID')
    ext["signing.password"] = System.getenv('SIGNING_PASSWORD')
    ext["signing.secretKeyRingFile"] = System.getenv('SIGNING_SECRET_KEY_RING_FILE')
    ext["ossrhUsername"] = System.getenv('OSSRH_USERNAME')
    ext["ossrhPassword"] = System.getenv('OSSRH_PASSWORD')
    ext["sonatypeStagingProfileId"] = System.getenv('SONATYPE_STAGING_PROFILE_ID')
}

publishing {
    repositories {
        maven {
            name = "sonatype"
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username ossrhUsername
                password ossrhPassword
            }
        }
    }
    publications.withType(MavenPublication) {

        groupId = publishGroupId
        artifactId publishArtifactId
        version publishVersion

        artifact javadocJar

        pom {
            name = libraryName
            description = libraryDescription
            url = 'https://github.com/nwagu/chess-boy'

            licenses {
                license {
                    name = 'GPL-3.0 License'
                    url = 'https://opensource.org/licenses/GPL-3.0'
                }
            }
            developers {
                developer {
                    id = 'nwagu'
                    name = 'Chukwuemeka Nwagu'
                    email = 'developer.nwagu@gmail.com'
                }
            }
            scm {
                url = 'https://github.com/nwagu/chess-boy'
            }
        }
    }
}

signing {
    sign publishing.publications
}